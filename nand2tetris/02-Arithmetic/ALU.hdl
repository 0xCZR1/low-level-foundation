// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
            //zx
        Mux16(a=x , b=false , sel=zx , out=zxOut );
            //nx
        Not16(in=zxOut, out=notX);
        Mux16(a=zxOut , b=notX , sel=nx , out=nxOut );
            
            //zy
        Mux16(a=y , b=false , sel=zy , out=zyOut );
            //ny
        Not16(in=zyOut , out=notY);
        Mux16(a=zyOut , b=notY , sel=ny , out=nyOut );
        
            //f
        Add16(a = nxOut, b = nyOut, out =addXY );
        And16(a= nxOut, b= nyOut , out=XandY );

        Mux16(a= XandY, b=addXY , sel=f , out=resultF );

            //no
        Not16(in=resultF , out=notresultF );
        Mux16(a=resultF , b=notresultF , sel=no , out=out, out[15]=out15, out[0..7]=outLeft, out[8..15]=outRight );

            //zr
        Or8Way(in=outLeft , out=zeroLow );
        Or8Way(in=outRight , out=zeroRight );
        Or(a=zeroLow, b=zeroRight, out=orZr);
        Not(in= orZr, out=zr);

            //ng
        And(a=out15, b=true , out=ng );

}
